PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX frbroo: <http://iflastandards.info/ns/fr/frbr/frbroo/>
PREFIX veniss_ontology: <https://veniss.net/ontology#>
select distinct ?item ?label ?author_lbl ?title_lbl ?broader_title_lbl ?place_lbl ?publisher_lbl ?year_lbl where {

  optional {
    ?item frbroo:R24i_was_created_through ?publication_event.
    optional {
    	?publication_event crm:P01i_is_domain_of ?carried_out_by_curator.
    	?carried_out_by_curator crm:P14.1_in_the_role_of <http://veniss.net/resource/type/curator>.
		?carried_out_by_curator crm:P02_has_range ?curator.
      	?curator rdfs:label ?curator_label
      bind(concat(", a cura di ", ?curator_label) as ?curator_label_final)
    }
    optional {
    	?publication_event crm:P01i_is_domain_of ?carried_out_by_publisher.
    	?carried_out_by_publisher crm:P14.1_in_the_role_of <http://veniss.net/resource/type/publisher>.
		?carried_out_by_publisher crm:P02_has_range ?publisher.
      	?publisher rdfs:label ?publisher_label
      bind(concat(", ", ?publisher_label) as ?publisher_final)
    }
    optional {
      ?publication_event crm:P4_has_time-span ?timespan.
      ?timespan crm:P82a_begin_of_the_begin ?year
      bind(concat(", ", ?year) as ?year_final)
    }
    optional {
      ?publication_event crm:P7_took_place_at ?place.
      ?place rdfs:label ?place_label
      bind(concat(", ", ?place_label) as ?place_final)
    }
  }

  optional {
  	?item crm:P129i_is_subject_of ?linguistic_object.
  	?linguistic_object crm:P94i_was_created_by ?creation.
  	?creation crm:P14_carried_out_by ?actor.
    ?actor rdfs:label ?author_final.
  }
  
  optional {
  	?item crm:P1_is_identified_by ?bibitem_identifier.
    ?bibitem_identifier crm:P2_has_type <https://veniss.net/resource/type/title>;
                        rdfs:label ?title
    bind(concat(", ", ?title) as ?title_final)
  }
  
  optional {
    ?item veniss_ontology:has_broader ?broader.
    ?broader crm:P1_is_identified_by ?broader_title.
    ?broader_title 	crm:P2_has_type <https://veniss.net/resource/type/title>;
		     		rdfs:label ?broader_title_label
    bind(concat(", ", ?broader_title_label) as ?broader_title_final)
  }
  
  bind(coalesce(?curator_label_final, "") as ?curator_lbl)
  bind(coalesce(?author_final, "") as ?author_lbl)
  bind(coalesce(?title_final, "") as ?title_lbl)
  bind(coalesce(?place_final, "") as ?place_lbl)
  bind(coalesce(?publisher_final, "") as ?publisher_lbl)
  bind(coalesce(?year_final, "") as ?year_lbl)
  bind(coalesce(?broader_title_final, "") as ?broader_title_lbl)
  
  bind(concat(?author_lbl, ?title_lbl, ?broader_title_lbl, ?curator_lbl, ?place_lbl, ?publisher_lbl, ?year_lbl) as ?label)
}