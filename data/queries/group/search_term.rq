PREFIX ontology: <http://dbpedia.org/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX crm:  <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX veniss_ontology: <https://veniss.net/ontology#>
PREFIX veniss_types: <https://veniss.net/resource/type/>

DELETE {
  ?item crm:P1_is_identified_by ?existingSt .
  ?existingSt ?p ?o .
}
INSERT {
  ?item crm:P1_is_identified_by ?st .
  ?st a crm:E41_Appellation ;
      crm:P2_has_type veniss_types:search_term ;
      rdfs:label ?label .
}
WHERE {
  ?item rdf:type veniss_ontology:Group .

  OPTIONAL {
    ?item crm:P1_is_identified_by ?existingSt .
    ?existingSt a crm:E41_Appellation ;
                crm:P2_has_type veniss_types:search_term .
    ?existingSt ?p ?o .
  }

  # ---- Name concatenation query ----
  OPTIONAL {
    SELECT ?item (SAMPLE(?value) AS ?name)
    WHERE {
      ?item a veniss_ontology:Group .
      
      OPTIONAL {
        ?item crm:P1_is_identified_by ?given_name_node.
        ?given_name_node crm:P2_has_type <https://veniss.net/resource/type/given_name>;
          rdfs:label ?given_name_label.
        BIND(?given_name_label AS ?given_name)
      }
      OPTIONAL {
        ?item crm:P1_is_identified_by ?family_name_node.
        ?family_name_node crm:P2_has_type <https://veniss.net/resource/type/family_name>;
          rdfs:label ?family_name_label.
        BIND(?family_name_label AS ?family_name)
      }
      OPTIONAL {
        ?item crm:P1_is_identified_by ?alias_node.
        ?alias_node crm:P2_has_type <https://veniss.net/resource/type/alias>;
          rdfs:label ?alias_label.
      }

      OPTIONAL {
        ?item crm:P1_is_identified_by ?appellation.
        ?appellation crm:P2_has_type <https://veniss.net/resource/type/appellation>;
          rdfs:label ?group_label
      }
      
      BIND(COALESCE(?given_name, "") AS ?given_name_final)
      BIND(COALESCE(CONCAT(?family_name, ", "), "") AS ?family_name_final)
      BIND(CONCAT(?family_name_final, ?given_name_final) as ?fullname_final)
      BIND(COALESCE(CONCAT(?alias_label, " (", ?fullname_final, ")"), ?fullname_final) AS ?person_name)
      BIND(COALESCE(?group_label, "") as ?group_name)
      
      BIND(CONCAT(?group_name, ?person_name) as ?value)
    }
    GROUP BY ?item
  }

  # ---- Formation year ----
  OPTIONAL {
    SELECT ?item (SAMPLE(STR(?formation_year_label)) AS ?formation_year)
    WHERE {
      ?item crm:P95i_was_formed_by ?formation.
      ?formation a crm:E66_Formation;
          crm:P4_has_time-span ?timespan.
      ?timespan crm:P170i_time_is_defined_by ?time_primitive.
      ?time_primitive a crm:E61_Time-Primitive;
          crm:P2_has_type <https://veniss.net/resource/type/time_primitive_year>;
          rdfs:label ?formation_year_label.
    }
    GROUP BY ?item
  }

  # ---- Dissolution year ----
  OPTIONAL {
    SELECT ?item (SAMPLE(STR(?dissolution_year_label)) AS ?dissolution_year)
    WHERE {
      ?item crm:P99i_dissolved_by ?dissolution.
      ?dissolution a crm:E68_Dissolution;
          crm:P4_has_time-span ?timespan.
      ?timespan crm:P170i_time_is_defined_by ?time_primitive.
      ?time_primitive a crm:E61_Time-Primitive;
          crm:P2_has_type <https://veniss.net/resource/type/time_primitive_year>;
          rdfs:label ?dissolution_year_label.
    }
    GROUP BY ?item
  }

  # ---- compose the final label ----
  BIND(CONCAT(
        COALESCE(?name, ""),
        IF(BOUND(?formation_year), CONCAT(" ", ?formation_year), ""),
        IF(BOUND(?dissolution_year), CONCAT(" ", ?dissolution_year), "")
      ) AS ?labelStr)

  BIND(?labelStr AS ?label)

  # ---- mint the new search_term node IRI ----
  BIND( IRI(CONCAT(STR(?item), "/search_term/", STRUUID())) AS ?st )
}