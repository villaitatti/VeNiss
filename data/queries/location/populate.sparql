PREFIX crm:  <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX veniss_types: <https://veniss.net/resource/type/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

INSERT {
  $subject crm:P1_is_identified_by ?parent_path .
  ?parent_path a crm:E41_Appellation ;
               crm:P2_has_type veniss_types:path ;
               rdfs:label ?path .
}
WHERE {
  $subject a crm:E78_Collection ;
           crm:P71i_is_listed_in <http://www.researchspace.org/resource/vocab/archives> ;
           crm:P46i_forms_part_of ?parent .

  # $subject must NOT already have a parent_path
  FILTER NOT EXISTS {
    $subject crm:P1_is_identified_by ?existingPP .
    ?existingPP a crm:E41_Appellation ;
                crm:P2_has_type veniss_types:path .
  }

  # parent MUST already have a parent_path → we append the SUBJECT's pref
  ?parent crm:P1_is_identified_by ?ppathApp .
  ?ppathApp a crm:E41_Appellation ;
            crm:P2_has_type veniss_types:path ;
            rdfs:label ?parent_path_sofar .

  # pick ONE preferred label for the CURRENT subject (acronym → it → untagged/en)
  {
    SELECT $subject (SAMPLE(?choice) AS ?subject_pref)
    WHERE {
      OPTIONAL {
        $subject crm:P1_is_identified_by ?subAcrApp .
        ?subAcrApp a crm:E41_Appellation ;
                   crm:P2_has_type veniss_types:acronym ;
                   rdfs:label ?sub_acr .
      }
      OPTIONAL { $subject rdfs:label ?sub_it  FILTER(lang(?sub_it)  = "it") }
      OPTIONAL { $subject rdfs:label ?sub_any FILTER(lang(?sub_any) = "" || lang(?sub_any) = "en") }
      BIND(COALESCE(?sub_acr, ?sub_it, ?sub_any) AS ?choice)
    }
    GROUP BY $subject
  }

  BIND(CONCAT(?parent_path_sofar, ", ", ?subject_pref) AS ?path)
  BIND(IRI(CONCAT(STR($subject), "/path")) AS ?parent_path)
}